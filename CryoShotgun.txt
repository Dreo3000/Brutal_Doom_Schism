// --------------------------------------------------------------------------
//
//	CryoShotgun
//
//	(c) Schroding Cat
//
//	Edited For Schism by Dreo
//
// --------------------------------------------------------------------------

ACTOR CryoShotgun : Shot_Gun
{
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 20
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "CryoShell"
	Weapon.AmmoType2 "CryoShotgunAmmo"
	//Weapon.SlotNumber 3
	Weapon.SelectionOrder 300
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+Powered_Up
	Weapon.SisterWeapon Shot_Gun
	Inventory.PickupSound "M1014RE"
	Inventory.Pickupmessage "You got the Cryo Shotgun!"
	Obituary "%o got frozen to death by %k's Cryo Shotgun."
	AttackSound "None"
	Tag "Cryo Shotgun"
	Scale 0.9
	Inventory.ForbiddenTo Paladin, Sorcerer
	States
	{
	Spawn:
		FZSG A -1
		Stop

	Select:
		TNT1 A 0 A_Giveinventory("ShotgunSelected",1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("TossGrenade",1)
		TNT1 A 0 A_Takeinventory("SwitchFlashlight",1)
		TNT1 A 0 A_Takeinventory("Taunting",1)
		TNT1 A 5
		TNT1 A 0 A_Raise
		Wait

	Deselect:
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1,"DeselectPistolSpecial")
		TNT1 A 0 A_PlaySound("weapons/changing")
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_TakeInventory("PistolSpecial",1)
		TNT1 A 0 A_Takeinventory("HasUnloaded",1)
		TNT1 A 0 A_Takeinventory("HasFreezerWeapon",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_SetCrosshair(0)
		FZGS EDCBA 1
		TNT1 A 1 A_Takeinventory("ShotgunSelected",1)
		TNT1 A 0 A_Lower
		Wait
		
	DeselectPistolSpecial:
		TNT1 A 0 A_PlaySound("weapons/changing")
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_Takeinventory("HasUnloaded",1)
		TNT1 A 0 A_Takeinventory("HasFreezerWeapon",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 A 0 A_GunFlash("PistolEndFlash")
		"####" ABCDEF 1
		"####" G 5
		FZGS EDCBA 1
		TNT1 A 1 A_Takeinventory("ShotgunSelected",1)
		TNT1 A 0 A_TakeInventory("PistolSpecial",1)
		TNT1 A 0 A_Lower
		Wait
		
	Ready:
		TNT1 A 0 A_JumpIfInventory("GoFatality",1,"Steady")
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1, "ReadyPistolSpecial")
		TNT1 A 0 A_PlaySound("M1014RE",5)
	Ready1:
		FZGS ABCDE 1
	Ready2:
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"SelectPistolSpecial")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ReadyZoom")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		TNT1 A 0 A_JumpIfInventory("Salute1",1,"Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2",1,"Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("Unloading",1,"Unload")
		FZGA A 1 A_WeaponReady
		Loop

	ReadyZoom:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Salute1",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Salute2",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Unloading",1,"ZoomOut")
		FZGA E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop

	
	ReadyPistolSpecial:
		TNT1 A 0 A_GunFlash("PistolFlash")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"WeapSpecial")
		5BEF A 1 A_WeaponReady
		Loop
		
	SelectPistolSpecial:
		TNT1 A 0 A_TakeInventory("StartDualWield",1)
		TNT1 A 0 A_GiveInventory("PistolSpecial", 1)
		//FZGH ABCDEFFFF 1
		TNT1 A 0 A_GunFlash("PistolStartFlash")
		5BES A 0
		"####" G 5 A_PlaySound("weapons/pistolup",2)
		"####" FEDCBA 1
		Goto ReadyPistolSpecial
		
		
// --------------------------------------------------------------------------
//
//	Fire
//
// --------------------------------------------------------------------------

	Fire:
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1,"FirePistol")
		TNT1 A 0 A_JumpIfInventory("ReloadingCryoShotty",2,"ReloadDone2")
		TNT1 A 0 A_JumpIfInventory("ReloadingCryoShotty",1,"ReloadDone1")
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",1,3)
		TNT1 A 0 A_JumpIfInventory("CryoShell",1,1)
		Goto DryFire
		TNT1 A 0
		Goto Reload
		TNT1 A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,5)
		TNT1 A 0 A_AlertMonsters
		//TNT1 A 0 A_PlaySound("CRYSGFI",1)
		TNT1 A 0 A_PlaySound("weapons/sg", 1)
		TNT1 A 0 A_FireCustomMissile("DistantFireSoundShotgun",random(-1,1),0,0,-12,0,random(-1,1))
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAss",0,0,0,0)
		TNT1 A 0 A_Takeinventory("CryoShotgunAmmo",1)	
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoom")
		//TNT1 AAA 0 A_FireCustomMissile("Tracer",frandom(-5,5),0,-1,-12,0,frandom(-5,5))
		FZGF A 1 BRIGHT A_Giveinventory("HasFreezerWeapon",1)
		TNT1 AAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile ("IceShardSmall",frandom(-1.5,1.5),0,0,0,0,frandom(-1.5,1.5))
		TNT1 A 0 A_SetPitch(-7.0 + pitch)
		FZGF B 1 BRIGHT A_SetPitch(1.65 + pitch)
		FZGF CEF 1 A_SetPitch(1.65 + pitch)  
		FZGA A 4
		FZGP AB 1
		FZGR ABCDE 1 
		FZGR F 1 A_PlaySound("weapons/sgpump",5,0.8)
		FZGR FGHIJ 1
		FZGR KL 1 //A_StopSound(5)
		TNT1 A 0 A_FireCustomMissile("CryoShotCaseSpawn",5,0,6,-24)
		FZGR MNEFFEDCBA 1
		FZGP B 1 A_Takeinventory("HasFreezerWeapon",1)
		FZGP A 1 A_ReFire
		Goto Ready2

	FireZoom:
		//TNT1 AAA 0 A_FireCustomMissile("Tracer",frandom(-3.3,3.3),0,-1,-12,0,frandom(-3.3,3.3))
		TNT1 A 0 A_GiveInventory("HasFiredWithZoom",1)
		TNT1 A 0 A_ZoomFactor(1.4)
		FZGA F 1 BRIGHT A_Giveinventory("HasFreezerWeapon",1)
		TNT1 AAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile ("IceShardSmall",frandom(-0.5,0.5),0,0,0,0,frandom(-0.5,0.5))
		TNT1 A 0 A_SetPitch(-6.2 + pitch)
		FZGA G 1 BRIGHT A_SetPitch(1.5 + pitch)
		TNT1 A 0 A_ZoomFactor(1.5)
		FZGA HIJ 1 A_SetPitch(1.5 + pitch)
		FZGA E 4
		FZGA K 2
		FZGA K 1 A_PlaySound("weapons/sgpump",5,0.8)
		FZGA LMN 1
		FZGA MLL 1 //A_StopSound(5)
		TNT1 A 0 A_FireCustomMissile("CryoShotCaseSpawn",5,0,4,-4)	
		FZGA K 2
		TNT1 A 0 A_Takeinventory("HasFreezerWeapon",1)
		TNT1 A 0 A_ReFire
		TNT1 A 0 //A_FireCustomMissile("SmokeSpawner11",0,0,0,0)
		FZGA E 1 A_WeaponReady
		Goto ReadyZoom
	
	FirePistol:
		TNT1 A 0 A_JumpIfInventory("BDPistolAmmo",1,3)
		TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
		Goto PistolDryFire
		TNT1 A 0
		Goto ReloadPistol
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,0,45)
		TNT1 A 0 A_FireCustomMissile("YellowFlareSpawn",0,0,15,0)
		TNT1 A 0 A_PlaySound("FARSHT",1)
		TNT1 A 0 A_PlaySound("PFIRE",1)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_PlaySound("PISFIRE",1,0.3)
		TNT1 A 0 A_PlaySound("SPIFIR",5)		
		TNT1 A 0 A_ZoomFactor(0.98)
		5BEF B 1 BRIGHT
		TNT1 A 0 A_Setpitch(pitch-0.6)
		TNT1 A 0 A_FireBullets(1.2,1.2,-1,9,"HitPuff",1)
		TNT1 A 0 A_FireCustomMissile("Tracer",frandom(-1,1),0,10,0,0,frandom(-1,1))
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssMinor",0,0,0,0)
		TNT1 A 0 A_Takeinventory("BDPistolAmmo",1)
		TNT1 A 0 A_Takeinventory("BDDualPistolAmmo",1)
		5BEF C 1
		TNT1 A 0 A_Setpitch(pitch+0.1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(1.00)
		TNT1 A 0 A_FireCustomMissile("PistolCaseSpawn",5,0,21,-8)
		Goto ReadyPistolSpecial
		
	AltFire:
		FZGA A 0
		TNT1 A 0 A_SetCrosshair(41)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1,"Fire")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomOut")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.5)
		FZGA A 0 A_Giveinventory("ADSmode",1)
		"####" ABCD 1
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		"####" E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		"####" E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		"####" E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		"####" E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		"####" E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		"####" E 2 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		TNT1 A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")
		Goto ReadyZoom
		
	HoldAim:	
		TNT1 A 0 A_JumpIfInventory ("HasFiredWithZoom",1,2)
		TNT1 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		TNT1 A 0 A_JumpIfInventory("FiredPrimary",1,2)
		TNT1 A 0 A_TakeInventory("HasFiredWithZoom",1)
		FZGA E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")

	ZoomOut:
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_SetCrosshair(0)
		FZGA DCB 1
		Goto Ready2


// --------------------------------------------------------------------------
//
//	Reload
//
// --------------------------------------------------------------------------

	Reload:
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1,"ReloadPistol")
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",8,"Ready2") //Is weapon full?
		TNT1 A 0 A_JumpIfInventory("CryoShell",1,1) //Do we have any spare ammo?
		Goto Ready2
		TNT1 A 0 A_JumpIfInventory("TurboReload",1,"TurboReloadLoop")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,2)
		TNT1 A 0 A_Jump(256,9)
		FZGA DCBA 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_GiveInventory("Pumping",1)
		FZGA A 1
		FZGP ABCDEFGGG 1
		TNT1 A 0 A_GiveInventory("ReloadingCryoShotty",1) //Reloading and weapon contains ammo
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",1,"ReloadLoop1")
		TNT1 A 0 A_GiveInventory("ReloadingCryoShotty",1) //Reloading and weapon was empty
		Goto ReloadLoop2
		
	TurboReloadLoop:
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",8,"TurboReloadDone")
		TNT1 A 0 A_JumpIfInventory("CryoShell",1,1)
		Goto TurboReloadDone
		TNT1 A 0 A_Takeinventory("CryoShell",1)
		TNT1 A 0 A_Giveinventory("CryoShotgunAmmo",1)
		Loop
		
	TurboReloadDone:
		TNT1 A 0 A_Refire
		Goto Ready2

	ReloadLoop1: //Weapon contains ammo when starting reload
		TNT1 A 0 A_JumpIfInventory("CryoShell",1,1)
		Goto ReloadDone1
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",8,"ReloadDone1")
		TNT1 A 0 A_GiveInventory("Pumping",1)
		FZGP HIJ 1
		FZGP K 1 A_PlaySound("Insertshell")
		TNT1 A 0 A_Takeinventory("CryoShell",1)
		TNT1 A 0 A_Giveinventory("CryoShotgunAmmo",1)
		FZGP LMN 1
		FZGP I 1 A_Refire
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"ReloadKick")
		Loop

	ReloadLoop2: //Weapon was empty when starting reload
		TNT1 A 0 A_JumpIfInventory("CryoShell",1,1)
		Goto ReloadDone2
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",8,"ReloadDone2")
		TNT1 A 0 A_GiveInventory("Pumping",1)
		FZGP HIJ 1
		FZGP K 1 A_PlaySound("Insertshell")
		TNT1 A 0 A_Takeinventory("CryoShell",1)
		TNT1 A 0 A_Giveinventory("CryoShotgunAmmo",1)
		FZGP LMN 1
		FZGP I 1 A_Refire
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"ReloadKick")
		Loop

	ReloadDone1: //Weapon contains ammo when starting reload
		FZGP FEDBC 1
		
		//TNT1 A 0 A_Takeinventory("CryoShell",1)
		//TNT1 A 0 A_Giveinventory("CryoShotgunAmmo",1)
		FZGR BC 1
		FZGR D 1 A_PlaySound("weapons/sgpump",5,0.8)
		FZGR EFGHIJ 1 
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("ReloadingCryoShotty",2)
		TNT1 A 0 A_Refire
		FZGR KLMNCBA 1 A_StopSound(5)
		FZGP BA 1
		Goto Ready2

	ReloadDone2: //Weapon was empty when starting reload
		TNT1 A 0 A_GiveInventory("Pumping",1)
		FZGP FEDBC 1
		FZGR BC 1
		FZGR D 1 A_PlaySound("weapons/sgpump",5,0.8)
		FZGR EFGHIJ 1 
		//TNT1 A 0 A_Takeinventory("CryoShell",1)
		//TNT1 A 0 A_Giveinventory("CryoShotgunAmmo",1)
		FZGR KLMNCBA 1 A_StopSound(5)
		FZGP BA 1
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("ReloadingCryoShotty",2)
		TNT1 A 0 A_Refire
		Goto Ready2

	ReloadKick:
		TNT1 A 0 A_JumpIfInventory("ReloadingCryoShotty",2,1)
		Goto ReloadKickAlt
		TNT1 A 0 A_GiveInventory("Pumping",1)
		FZGP IJK 1
		TNT1 A 0 A_PlaySound("M1014RE",5)
		TNT1 A 0 A_Takeinventory("CryoShell",1)
		TNT1 A 0 A_Giveinventory("CryoShotgunAmmo",1)
		FZGP LLMN 1
	ReloadKickAlt:
		FZGP I 2
		TNT1 A 0 A_GunFlash("ReloadKickFlash")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_Jumpifinventory("PowerStrength",1,3)
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		KICK H 5
		KICK IGFEDCBA 1 
		TNT1 A 0 SetPlayerProperty(0,0,0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("ReloadingCryoShotty",2)
		TNT1 A 0 A_JumpIfInventory("GoFatality",1,"Steady")
		Goto Ready2
	
	ReloadPistol:
		TNT1 A 0 A_JumpIfInventory("BDPistolAmmo",16,"ReadyPistolSpecial")//Don't reload if magazine is full,and there is a bullet in the chamber.
		TNT1 A 0 A_JumpIfInventory("Clip1",1,1)//Do we have any ammo to reload with?
		Goto ReadyPistolSpecial
		TNT1 A 0 A_PlaySound("prel")
		5BES A 0
		"####" ABCDDEE 1
		"####" F 16
		"####" EEDDCBA 1
	ReloadPistolLoop:
		TNT1 A 0 A_JumpIfInventory("BDPistolAmmo",16,"ReloadPistolDone")
		TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
		Goto ReloadPistolDone
		TNT1 A 0 A_Giveinventory("BDPistolAmmo",1)
		TNT1 A 0 A_Giveinventory("BDDualPistolAmmo",1)
		TNT1 A 0 A_TakeInventory("Clip1",1)
		Goto ReloadPistolLoop
	ReloadPistolDone:
		TNT1 A 0 A_Refire
		Goto ReadyPistolSpecial
		
// --------------------------------------------------------------------------
//
//	Unload
//
// --------------------------------------------------------------------------

	Unload:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",1,1) //We have any ammo to unload?
		Goto Ready2
		TNT1 A 0 A_GiveInventory("Pumping",1)
		FZGP A 1
		FZGP BCDEFGH 1
		//M4ZR AFEE 2
		//TNT1 A 0 A_PlaySound("M1014RE",5)
		//TNT1 A 0 A_GiveInventory("CryoShell",1)
		//TNT1 A 0 A_Takeinventory("CryoShotgunAmmo",1)
		//M4ZR DCBA 2
		Goto UnloadLoop
		
	UnloadLoop:
		TNT1 A 0 A_JumpIfInventory("CryoShotgunAmmo",1,1)
		Goto UnloadDone
		TNT1 A 0 A_GiveInventory("Pumping",1)
		//M4HR ABC 1
		//M4HR D 1 A_PlaySound("Insertshell")
		FZGP NML 1
		TNT1 A 0 A_PlaySound("Insertshell")
		TNT1 A 0 A_GiveInventory("CryoShell",1)
		TNT1 A 0 A_Takeinventory("CryoShotgunAmmo",1)
		//M4HR EFGHI 1
		FZGP KJ 1
		FZGP I 1
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"ReloadKick")
		Loop
		
	UnloadDone:
		FZGP HGFEDCB 1
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Refire
		Goto Ready2


// --------------------------------------------------------------------------
//
//	Misc.
//
// --------------------------------------------------------------------------
	
	DryFire:
		TNT1 A 0 A_PlaySound("weapons/empty2",2)
		Goto Ready2	
		
    PistolDryFire:
		TNT1 A 0 A_PlaySound("weapons/empty2")
		Goto ReadyPistolSpecial
		
	WeapSpecial:
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"WeapSpecialBack")
		Goto Ready2
	
	WeapSpecialBack:
		TNT1 A 0 A_TakeInventory("StartDualWield",1)
		TNT1 A 0 A_TakeInventory("PistolSpecial", 1)
		TNT1 A 0 A_GunFlash("PistolEndFlash")
		5BES A 0
		"####" ABCDEF 1
		"####" G 5
		Goto Ready2
		
	PuristGun:
		TNT1 A 0 A_TakeInventory("CryoShotgun",1)
		Goto Deselect


	Steady:
		TNT1 A 1
		Goto Ready

// --------------------------------------------------------------------------
//
//	End stuff
//
// --------------------------------------------------------------------------

	TauntFlash:
		FZGH ABCDE 1
		FZGH F 34
		FZGH EDCBA 1
		FZGA A 1
		Stop

	KickFlash:
		FZGH ABCDE 1
		FZGH F 6
		FZGH EDCBA 1
		FZGA A 1
		Stop

	AirKickFlash:
		FZGH ABCDE 1
		FZGH F 8
		FZGH EDCBA 1
		FZGA A 1
		Stop

	SlideKickStartFlash:
		FZGH ABCDE 1
		FZGH F 16
		Stop
	
	SlideKickEndFlash:
		FZGH EDCBA 2
		FZGA A 1
		Stop

	ReloadKickFlash:
		FZGH AB 2
		FZGH C 9
		FZGH DEF 1
		FZGH A 1
		Stop

	GrenadeFlash:
		FZGS EDCBA 1
		Stop
	
	PistolStartFlash:
		FZGH ABCDEFFFFFF 1
		Stop
	
	PistolFlash:
		FZGH F -1
		Stop
		
	PistolEndFlash:
		FZGH FFFFFFEDCBA 1
		Stop
	}
}

ACTOR CryoShotgunAmmo: Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 0
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8
   	Inventory.Icon "FZSGA0"
}

Actor PistolSpecial : Inventory { Inventory.MaxAmount 1 }
Actor ReloadingCryoShotty : Inventory { Inventory.MaxAmount 2 }